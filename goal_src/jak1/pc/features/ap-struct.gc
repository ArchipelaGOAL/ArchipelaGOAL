;;-*-Lisp-*-
(in-package goal)
(define *ap-info-jak1* (new 'static 'ap-info-jak1))

(define *ap-string1* (new 'global 'string 256 (the string #f)))

(define *ap-string2* (new 'global 'string 256 (the string #f)))

;; Init arrays
;;(set! (-> *ap-info-jak1* cells) (new 'static 'boxed-array :type uint32 :length 101 :allocated-length 101))
;;(set! (-> *ap-info-jak1* buzzers) (new 'static 'boxed-array :type uint32 :length 112 :allocated-length 112))
;; Setup markers
(charp<-string (-> *ap-info-jak1* end-marker) "end")

;; ****************************************************************************
;; **** This number must match expected_memory_version in MemoryReader.py! ****
;; ****************************************************************************
(set! (-> *ap-info-jak1* version) 5)

;; These don't need to be indexed like they are game-tasks.
;; We can just take them first-come-first-serve.
(defun ap-cell-checked! ((cell-id uint))
  (set! (-> *ap-info-jak1* cells-checked (-> *ap-info-jak1* next-cell-index)) cell-id)
  (+! (-> *ap-info-jak1* next-cell-index) 1.0))

(defun ap-buzzer-checked! ((buzzer-id uint))
  (set! (-> *ap-info-jak1* buzzers-checked (-> *ap-info-jak1* next-buzzer-index)) buzzer-id)
  (+! (-> *ap-info-jak1* next-buzzer-index) 1.0))

(defun ap-special-checked! ((special-id uint))
  (set! (-> *ap-info-jak1* specials-checked (-> *ap-info-jak1* next-special-index)) special-id)
  (+! (-> *ap-info-jak1* next-special-index) 1.0))

(defun ap-special-checked? ((special-id uint))
  (let ((checked #f))
    (dotimes (i (-> *ap-info-jak1* next-special-index))
      (when (= special-id (-> *ap-info-jak1* specials-checked i))
        (set! checked #t)))
    checked))

(defun ap-orb-cache-checked! ((actor-id uint))
  (set! (-> *ap-info-jak1* orb-caches-checked (-> *ap-info-jak1* next-orb-cache-index)) actor-id)
  (+! (-> *ap-info-jak1* next-orb-cache-index) 1.0))

(defun ap-orb-cache-checked? ((actor-id uint))
  (let ((checked #f))
    (dotimes (i (-> *ap-info-jak1* next-orb-cache-index))
      (when (= actor-id (-> *ap-info-jak1* orb-caches-checked i))
        (set! checked #t)))
    checked))

;; These will be indexed like game-tasks, so make sure we set the right index!

;; These are the levels' buzz-tasks in the same order as defined by the archipelago
;; locations lists.
(defun ap-buzzer-task-to-index ((buzz-task uint))
  (cond
    ((= buzz-task 95) (the-as uint 0)) ;; GR
    ((= buzz-task 75) (the-as uint 1)) ;; SV
    ((= buzz-task 7) (the-as uint 2)) ;; FJ
    ((= buzz-task 20) (the-as uint 3)) ;; SB
    ((= buzz-task 28) (the-as uint 4)) ;; MI
    ((= buzz-task 68) (the-as uint 5)) ;; FC
    ((= buzz-task 76) (the-as uint 6)) ;; RV
    ((= buzz-task 57) (the-as uint 7)) ;; PB
    ((= buzz-task 49) (the-as uint 8)) ;; LPC
    ((= buzz-task 43) (the-as uint 9)) ;; BS
    ((= buzz-task 88) (the-as uint 10)) ;; MP
    ((= buzz-task 77) (the-as uint 11)) ;; VC
    ((= buzz-task 85) (the-as uint 12)) ;; SC
    ((= buzz-task 65) (the-as uint 13)) ;; SM
    ((= buzz-task 90) (the-as uint 14)) ;; LT
    ((= buzz-task 91) (the-as uint 15)) ;; GMC
    ))

(defun ap-buzzer-received! ((buzz-task uint) (buzz-index uint))
  (+! (-> *ap-info-jak1* buzzers-received (ap-buzzer-task-to-index buzz-task)) 1.0))

(defun ap-buzzer-count ((buzz-task uint))
  (-> *ap-info-jak1* buzzers-received (ap-buzzer-task-to-index buzz-task)))

(defun ap-buzzer-specific-7? ((buzz-task uint))
  (= (ap-buzzer-count buzz-task) 7))

;; Whereas specific-7 checks that you received all 7 buzzers for a single level,
;; progressive-7 asks if you received any 7 buzzers, so you will always receive 
;; a power cell every 7 flies, distributing their power cells more evenly 
;; throughout a run and making the game less RNG-heavy.
(defun ap-buzzer-progressive-7? ()
  (let ((sum 0)) (dotimes (i 16) (+! sum (-> *ap-info-jak1* buzzers-received i))) (and (nonzero? sum) (= (mod sum 7) 0))))

;; These are the game-tasks in the same order as defined by the archipelago
;; special location list.
(defun ap-special-task-to-index ((special-id uint))
  (cond
    ((= special-id 5) (the-as uint 0)) ;; Fisherman's Boat
    ((= special-id 4) (the-as uint 1)) ;; Jungle Elevator
    ((= special-id 2) (the-as uint 2)) ;; Blue Eco Switch
    ((= special-id 17) (the-as uint 3)) ;; Flut Flut
    ((= special-id 33) (the-as uint 4)) ;; Warrior's Pontoons
    ((= special-id 105) (the-as uint 5)) ;; Snowy Mountain Gondola (village3-button)
    ((= special-id 60) (the-as uint 6)) ;; Yellow Eco Switch
    ((= special-id 63) (the-as uint 7)) ;; Snowy Fort Gate
    ((= special-id 71) (the-as uint 8)) ;; Freed The Blue Sage
    ((= special-id 72) (the-as uint 9)) ;; Freed The Red Sage
    ((= special-id 73) (the-as uint 10)) ;; Freed The Yellow Sage
    ((= special-id 70) (the-as uint 11)) ;; Freed The Green Sage
    ))

(defun ap-special-received! ((special-id uint))
  (send-event (ppointer->process (-> *hud-parts* fuel-cell)) 'show)
  (sound-play "buzzer-pickup" :pitch 0.166)
  (set! (-> *ap-info-jak1* specials-received (ap-special-task-to-index special-id)) (the uint 1)))

(defun ap-special-received? ((special-id uint))
  (> (-> *ap-info-jak1* specials-received (ap-special-task-to-index special-id)) 0))

;; These are the move ID's in the same order as defined by the archipelago
;; orb cache location list/move item table.
(defun ap-move-id-to-index ((move-id uint))
  (cond
    ((= move-id 10344) (the-as uint 0)) ;; Crouch
    ((= move-id 10369) (the-as uint 1)) ;; Crouch Jump
    ((= move-id 11072) (the-as uint 2)) ;; Crouch Uppercut
    ((= move-id 12634) (the-as uint 3)) ;; Roll
    ((= move-id 12635) (the-as uint 4)) ;; Roll Jump
    ((= move-id 10945) (the-as uint 5)) ;; Double Jump
    ((= move-id 14507) (the-as uint 6)) ;; Jump Dive
    ((= move-id 14838) (the-as uint 7)) ;; Jump Kick
    ((= move-id 23348) (the-as uint 8)) ;; Punch
    ((= move-id 23349) (the-as uint 9)) ;; Punch Uppercut
    ((= move-id 23350) (the-as uint 10)) ;; Kick
    ))

(defun ap-move-received! ((move-id uint))
  (send-event (ppointer->process (-> *hud-parts* fuel-cell)) 'show)
  (sound-play "buzzer-pickup" :pitch 0.166)
  (set! (-> *ap-info-jak1* moves-received (ap-move-id-to-index move-id)) (the uint 1)))

(defun ap-move-received? ((move-id uint))
  (> (-> *ap-info-jak1* moves-received (ap-move-id-to-index move-id)) 0))

(defun ap-deathlink-received! ((chosen-death symbol))
  (if (= (-> *target* state) target-fishing)
    (send-event (process-by-ename "fisher-1") 'deathlink) ;; quickhack for fishgame. 
    (send-event *target*
                'attack
                #f ;; everything else.
                (static-attack-info ((deathlink-death #t) (mode chosen-death) (vector (new 'static 'vector :y (meters 1) :w 1.0)) (angle 'up) (control 1.0)))))
  #t)

(defun ap-trap-received! ((trap-id uint))
  (when (and (> (-> *ap-info-jak1* trap-duration) 0.0)
          (zero? (get-task-status (game-task intro))))
    (sound-play "warning" :pitch -0.25)
    (cond
      ((= trap-id 1) ;; Trip Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (send-event *target* 'loading)
            (if (> (-> *ap-info-jak1* trap-duration) 5.0)
              (dotimes (i (- (/ (the int (-> *ap-info-jak1* trap-duration)) 5) 1))
                (suspend-for (seconds 5))
                (send-event *target* 'loading))))))
      ((= trap-id 2) ;; Slippery Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (set! (-> *stone-surface* slope-slip-angle) 16384.0)
            (set! (-> *stone-surface* slip-factor) 0.7)
            (set! (-> *stone-surface* transv-max) 1.5)
            (set! (-> *stone-surface* turnv) 0.5)
            (set! (-> *stone-surface* nonlin-fric-dist) 4091904.0)
            (set! (-> *stone-surface* fric) 23756.8)
            (set! (-> *grass-surface* slope-slip-angle) 16384.0)
            (set! (-> *grass-surface* slip-factor) 0.7)
            (set! (-> *grass-surface* transv-max) 1.5)
            (set! (-> *grass-surface* turnv) 0.5)
            (set! (-> *grass-surface* nonlin-fric-dist) 4091904.0)
            (set! (-> *grass-surface* fric) 23756.8)
            (set! (-> *ice-surface* slip-factor) 0.3)
            (set! (-> *ice-surface* nonlin-fric-dist) 8183808.0)
            (set! (-> *ice-surface* fric) 11878.4)
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (set! (-> *stone-surface* slope-slip-angle) 8192.0)
            (set! (-> *stone-surface* slip-factor) 1.0)
            (set! (-> *stone-surface* transv-max) 1.0)
            (set! (-> *stone-surface* turnv) 1.0)
            (set! (-> *stone-surface* nonlin-fric-dist) 5120.0)
            (set! (-> *stone-surface* fric) 153600.0)
            (set! (-> *grass-surface* slope-slip-angle) 16384.0)
            (set! (-> *grass-surface* slip-factor) 1.0)
            (set! (-> *grass-surface* transv-max) 1.0)
            (set! (-> *grass-surface* turnv) 1.0)
            (set! (-> *grass-surface* nonlin-fric-dist) 4096.0)
            (set! (-> *grass-surface* fric) 122880.0)
            (set! (-> *ice-surface* slip-factor) 0.7)
            (set! (-> *ice-surface* nonlin-fric-dist) 4091904.0)
            (set! (-> *ice-surface* fric) 23756.8)
            )))
      ((= trap-id 3) ;; Gravity Trap - Needs tweaking.
        (process-spawn-function process
          (lambda :behavior process ()
            (*! (-> *standard-dynamics* gravity-length) 3.0)
            (set! (-> *TARGET-bank* fall-far) (meters 2.5))
            (set! (-> *TARGET-bank* fall-far-inc) (meters 3.5))
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (*! (-> *standard-dynamics* gravity-length) 0.333)
            (set! (-> *TARGET-bank* fall-far) (meters 30))
            (set! (-> *TARGET-bank* fall-far-inc) (meters 20)))))
      ((= trap-id 4) ;; Camera Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (send-event *target* 'no-look-around (seconds (-> *ap-info-jak1* trap-duration)))
            (send-event *camera* 'change-state cam-circular 0)
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (send-event *target* 'no-look-around (seconds 0))
            (send-event *camera* 'change-state cam-string 0))))
      ((= trap-id 5) ;; Darkness Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (set! (-> (level-get-target-inside *level*) mood) *darkcave-mood*)
            (set! (-> (level-get-target-inside *level*) mood-func) update-mood-finalboss)
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (set! (-> (level-get-target-inside *level*) mood) *default-mood*)
            (set! (-> (level-get-target-inside *level*) mood-func) update-mood-default))))
      ((= trap-id 6) ;; Earthquake Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (suspend-for (seconds 1))
            (sound-play "explosion" :pitch -2.5)
            (activate! *camera-smush-control* 3000.0 50 (the int (seconds (-> *ap-info-jak1* trap-duration))) 1.0 1.0)
            (dotimes (i (* 100 (the int (-> *ap-info-jak1* trap-duration))))
              (set! (-> *standard-dynamics* gravity x) (* 3.0 (the float (update! *camera-smush-control*))))
              (suspend-for (seconds 0.01))
              (set! (-> *standard-dynamics* gravity z) (* 3.0 (the float (update! *camera-smush-control*))))
              (suspend-for (seconds 0.01))))))
      ((= trap-id 7) ;; Teleport Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (suspend-for (seconds 1))
            (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "village1-warp"))
            (initialize! *game-info* 'die (the-as game-save #f) "village1-warp"))))
      ((= trap-id 8) ;; Despair Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (let ((curr-lang (the-as language-enum (get-game-language *pc-settings*))))
              (set-game-language! *pc-settings* (language-enum japanese))
              (kill-current-level-hint '() '() 'exit)
              (suspend-for (seconds 1))
              (ambient-hint-spawn "WAR-LO1B" (the-as vector #f) *entity-pool* 'ambient)
              (if (> (-> *ap-info-jak1* trap-duration) 10.0)
                (dotimes (i (- (/ (the int (-> *ap-info-jak1* trap-duration)) 10) 1))
                  (suspend-for (seconds 10.3))
                  (kill-current-level-hint '() '() 'exit)
                  (suspend-for (seconds 0.1))
                  (ambient-hint-spawn "WAR-LO1B" (the-as vector #f) *entity-pool* 'ambient)))
            (set-game-language! *pc-settings* curr-lang)))))
      ((= trap-id 9) ;; Pacifism Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (set! (-> *TARGET-bank* punch-radius) (meters -1.0))
            (set! (-> *TARGET-bank* spin-radius) (meters -1.0))
            (set! (-> *TARGET-bank* flop-radius) (meters -1.0))
            (set! (-> *TARGET-bank* uppercut-radius) (meters -1.0))
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (set! (-> *TARGET-bank* punch-radius) (meters 1.3))
            (set! (-> *TARGET-bank* spin-radius) (meters 2.2))
            (set! (-> *TARGET-bank* flop-radius) (meters 1.4))
            (set! (-> *TARGET-bank* uppercut-radius) (meters 1.0)))))
      ((= trap-id 10) ;; Ecoless Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (send-event *target* 'reset-pickup 'eco)
            (set! (-> *FACT-bank* eco-full-timeout) (seconds 0.1))
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (set! (-> *FACT-bank* eco-full-timeout) (seconds 20.0)))))
      ((= trap-id 11) ;; Health Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (let ((curr-hp (-> *target* fact health)))
              (set! (-> *target* fact health) 0.0)
              (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
              (set! (-> *target* fact health) curr-hp)))))
      ((= trap-id 12) ;; Ledge Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (set! (-> *collide-edge-work* max-dir-cosa-delta) 999.0)
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (set! (-> *collide-edge-work* max-dir-cosa-delta) 0.6))))
      ((= trap-id 13) ;; Zoomer Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (send-event *target* 'change-mode 'racing #f)
            (suspend-for (max (seconds 2) (seconds (-> *ap-info-jak1* trap-duration)))) ;; animation takes more time
            (send-event *target* 'end-mode))))
      ((= trap-id 14) ;; Mirror Trap
        (process-spawn-function process
          (lambda :behavior process ()
            (logxor! (-> *pc-settings* cheats) 256) ;; the mirror cheat as a bitflag
            (suspend-for (seconds (-> *ap-info-jak1* trap-duration)))
            (logxor! (-> *pc-settings* cheats) 256))))
    ))
  (and (> (-> *ap-info-jak1* trap-duration) 0.0)
    (zero? (get-task-status (game-task intro)))))

(defun ap-setup-options! ((options ap-seed-options))
  (set! (-> *ap-settings* orbsanity-option) (-> options orbsanity-option))
  (set! (-> *ap-settings* orbsanity-bundle) (-> options orbsanity-bundle))
  (set! (-> *ap-settings* fire-canyon-unlock) (-> options fire-canyon-unlock))
  (set! (-> *ap-settings* mountain-pass-unlock) (-> options mountain-pass-unlock))
  (set! (-> *ap-settings* lava-tube-unlock) (-> options lava-tube-unlock))
  (set! (-> *ap-settings* citizen-orb-amount) (-> options citizen-orb-amount))
  (set! (-> *ap-settings* oracle-orb-amount) (-> options oracle-orb-amount))
  (set! (-> *ap-settings* trap-duration) (-> options trap-duration))
  (set! (-> *ap-settings* completion-goal) (-> options completion-goal))
  #t)

(defun ap-set-connection-status! ((status connection-status))
  (set! (-> *ap-info-jak1* connection-status) (the-as uint status))
  #t)

(defun ap-settings-update ()
  "A per frame update for settings related stuff"
  (set! (-> *ap-info-jak1* deathlink-enabled) (if (-> *ap-settings* deathlink?) 1 0))
  (set! (-> *ap-info-jak1* moverando-enabled) (if (-> *ap-settings* moverando?) 1 0))
  (set! (-> *ap-info-jak1* orbsanity-option) (-> *ap-settings* orbsanity-option))
  (set! (-> *ap-info-jak1* orbsanity-bundle) (-> *ap-settings* orbsanity-bundle))
  (set! (-> *ap-info-jak1* fire-canyon-unlock) (-> *ap-settings* fire-canyon-unlock))
  (set! (-> *ap-info-jak1* mountain-pass-unlock) (-> *ap-settings* mountain-pass-unlock))
  (set! (-> *ap-info-jak1* lava-tube-unlock) (-> *ap-settings* lava-tube-unlock))
  (set! (-> *ap-info-jak1* citizen-orb-amount) (-> *ap-settings* citizen-orb-amount))
  (set! (-> *ap-info-jak1* oracle-orb-amount) (-> *ap-settings* oracle-orb-amount))
  (set! (-> *ap-info-jak1* trap-duration) (-> *ap-settings* trap-duration))
  (set! (-> *ap-info-jak1* completion-goal) (-> *ap-settings* completion-goal))
  (none))
